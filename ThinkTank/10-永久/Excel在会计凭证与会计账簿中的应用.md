
2024-03-06

[[《Excel在会计中的应用》课程教学大纲]]-凭证与账簿
**一、科目代码表**
（一）实验目的
了解科目代码表、现金流量类型的设置方法和用途。
（二）实验要求
了解表对象的使用方法。
（三）实验数据
XX公司是一家商品流通企业，公司为一般纳税人，2017年年底，各个会计科目的余额如下表所示。

|   |   |   |
|---|---|---|
|科目代码|科目名称|2017年期末数|
|1001|现金|3630.00|
|100201|工行|1670421.94|
|100202|农行|502150.00|
|112201|海风公司|104060.00|
|112202|欧丽公司|154880.00|
|112203|新通公司|283140.00|
|112204|和信科技公司|135520.00|
|1231|坏账准备|3388.00|
|140501|JP32|25168.00|
|140502|JP33|28314.00|
|140503|JP34|195052.00|
|140504|JP35|551760.00|
|140505|MT3|453750.00|
|140506|MT4|275880.00|
|140507|MT5|314600.00|
|140508|KK2|7623.00|
|140509|KK3|54450.00|
|140510|KK4|29403.00|
|1601|固定资产|3097600.00|
|1602|累计折旧|612543.14|
|1801|长期待摊费用|145200.00|
|1811|递延所得税资产|688393.20|
|2001|短期借款|605000.00|
|220201|合力企业|145200.00|
|220202|一通公司|615890.00|
|220203|兴乐公司|218620.38|
|2211|应付职工薪酬|378659.82|
|222101|应交增值税（进项）|215989.87|
|222102|应交增值税（销项）|262069.42|
|222108|应交教育附加款|1291.95|
|2241|其他应付款|44322.30|
|4001|实收资本|6050000.00|

（四）实验要求
（1）根据上表的内容设置科目代码表，并为各个科目设置年初数。
（2）设置现金流量表的类型及代码。
（五）实验步骤
**A、科目代码表**
1、打开Excel，将工作表保存为“账务处理.xlsm”，并将工作表“sheet1”重命名为“科目代码表”。
2、在A1~E1单元格区域内依次输入字段名“科目代码、科目名称、性质、是否明细、年初数”。
3、选中A列并右击，执行“设置单元格格式”，将数字格式设为“小数位为0”。
4、选中E列，将单元格格式设为“会计专用、无货币符号”。
5、选中A1单元格，打开“插入-表格”内的“表格”命令，打开“创建表”对话框，勾选“表包含标题”复选框，“表数据来源”标题下设为“$A$1:$E$2”。
6、点击A1单元格，选中“设计”将“属性”组内的默认表明改为“kmdm”，完成表对象设计。
7、选中A1单元格，在“设计-表样式”选项卡中选择合适的样式，如样式9，完成表样式选择。
8、按照资料依次输入各个科目及相关数据。
最终完成后的科目代码表如下图所示。

|   |   |   |   |   |
|---|---|---|---|---|
|**科目代码**|**科目名称**|**性质**|**是否明细**|**年初数**|
|1001|现金|1|y|3,630.00|
|1002|银行存款|1|n||
|100201|工行|1|y|1,670,421.94|
|100202|农行|1|y|502,150.00|
|1012|其他货币资金|1|y|-|
|1101|交易性金融资产|1|y|-|
|1121|应收票据|1|y|-|
|1122|应收帐款|1|n|-|
|112201|海尚公司|1|y|104,060.00|
|112202|欧丽公司|1|y|154,880.00|
|112203|新通讯公司|1|y|283,140.00|
|112204|金鑫公司|1|y|135,520.00|
|1221|其他应收款|1|n|-|
|122101|张深|1|y|-|
|122102|方乐|1|y|-|
|122103|唐里|1|y|-|
|122104|侯光仁|1|y|-|
|1231|坏账准备|-1|y|3,388.00|
|1402|在途物资|1|y|-|
|1403|材料|1|y|-|
|1405|库存商品|1|n|-|
|140501|JP32|1|y|25,168.00|
|140502|JP33|1|y|28,314.00|
|140503|JP34|1|y|195,052.00|
|140504|JP35|1|y|551,760.00|
|140505|MT3|1|y|453,750.00|
|140506|MT4|1|y|275,880.00|
|140507|MT5|1|y|314,600.00|
|140508|KK2|1|y|7,623.00|
|140509|KK3|1|y|54,450.00|
|140510|KK4|1|y|29,403.00|
|1601|固定资产|1|y|3,097,600.00|
|1602|累计折旧|-1|y|612,543.14|
|1604|在建工程|1|y|-|
|1605|工程物资|1|y|-|
|1606|固定资产清理|1|y|-|
|1701|无形资产|1|y|-|
|1801|长期待摊费用|1|y|145,200.00|
|1811|递延所得税资产|1|y|688,393.20|
|2001|短期借款|-1|y|605,000.00|
|2201|应付票据|-1|y|-|
|2202|应付帐款|-1|n|-|
|220201|合力企业|-1|y|145,200.00|
|220202|一通公司|-1|y|615,890.00|
|220203|兴乐公司|-1|y|218,620.38|
|2211|应付职工薪酬|-1|y|378,659.82|
|2232|应付股利|-1|y|-|
|2221|应交税费|-1|n|-|
|222101|应交增值税（进项）|1|y|215,989.87|
|222102|应交增值税（销项）|-1|y|262,069.42|
|222103|应交消费税|-1|y|-|
|222104|应交营业税|-1|y|-|
|222105|应交城建税|-1|y|-|
|222106|应交个人所得税|-1|y|-|
|222107|应交所得税|-1|y|-|
|222108|应交教育附加款|-1|y|1,291.95|
|2241|其他应付款|-1|y|44,322.30|
|4001|实收资本|-1|y|6,050,000.00|
|4002|资本公积|-1|y|-|
|4101|盈余公积|-1|y|-|
|4103|本年利润|-1|y|-|
|4104|利润分配|-1|y|-|
|5001|生产成本|-1|y|-|
|5101|制造费用|-1|y|-|
|6001|主营业务收入|-1|y|-|
|6051|其他业务收入|-1|y|-|
|6111|投资收益|-1|y|-|
|6301|营业外收入|-1|y|-|
|6401|主营业务成本|1|y|-|
|6402|其他业务支出|1|y|-|
|6403|主营业务税金及附加|1|y|-|
|6601|销售费用|1|y|-|
|6602|管理费用|1|n|-|
|660201|业务招待费|1|y|-|
|660202|工资|1|y|-|
|660203|福利费|1|y|-|
|660204|其他|1|y|-|
|660205|劳保费用|1|y|-|
|6603|财务费用|1|y|-|
|6711|营业外支出|1|y|-|
|6801|所得税费用|1|y|-|
|6901|以前年度损益调整|1|y|-|

9、点击“公式-定义的名称-定义名称”命令，打开“新建名称”对话框，在“名称”后输入“dm”，在引用位置中输入“=kmdm[科目代码]”，完成第一列名称的设置。
**B、现金流量类型**
1、新建一张工作表，并命名为“参数”。
2、在A1:B1单元区域输入“内容、类型代码”。
3、选中A1单元格，选择“插入-表格”的“表格”命令打开“创建表”对话框，设定数据来源“=$A$1：$B$1”，勾选“表包含标题”，点击“确定”按钮创建表对象。
4、选中A1单元格，选中“设计”选项卡，在“属性”组内将表名更改为“xjll”。
5、从A2单元格开始输入资料中的内容和现金流量代码。
6、点击“公式”选项卡，执行“定义的名称-定义名称”打开“新建名称”对话框，在“名称”中输入“现金流量类型”，“引用位置”设置中输入“=xjll[内容]”，点击“确定”，完成新建名称的设置。
**二、凭证输入**
（一）实验目的
了解记账凭证和后台数据表之间的联系。
（二）实验要求
了解表对象的使用方法，了解宏的录制过程。
（三）实验数据
在2018年1月，XX公司发生了如下业务：
(1)购买编号为P34的商品33000元，购买MT5的商品21000元，购买编号为KK2的商品8000元，增值税税率为17%，以工行支付货款。
(2)向海尚公司销售P32和P33两种商品，收入为175000元，其中工行收到了货款13475元，其余部分稍后支付。P32成本为6000元，JP33成本为5000元。
(3)工行收到欧丽公司归还的货款88000元。
(4)归还一通公司货款250000元，货款已经通过工行支付。
(5)销售P35、MT5和KK3三种商品共计152800元，工行己经收到全部货款。JP35的成本为61000元，MT5的成本为28000元，KK2的成本为3350元。
(6)现金支付销售部门费用98元。
(7)工行支付招待费5532元。
(8)向金鑫公司销售商品MT4共计254000元，未收到货款。MT4商品成本为189100元。
(9)销售部门购买办公用品2200元，以工行支付。
(10)销售P34商品157200元，货款已经通过工行收讫，JP34商品的成本为72500元。
(11)以工行支付税款，上月应交增值税销项税71382元，不考虑增值税外的其他税收。
(12)购买商品MT4共计487000元，P32共计45000元，JP34共计102000元，货款未支付，其中从合力赊购573300元，从兴乐公司赊购168480元。
(13)工行支付前期计入其他应付款的水费6022元。
(14)购买空调5台，共计35000元，以工行支付。
(15)方乐出差暂领现金3500元。
(16)从工行提取175000元支付职工工资，其中销售部门95000元，管理部门80000元。同时按10%计提福利费用。
(17)工行收到新通讯公司归还的欠款100000元。
(18)出售商品KK3和KK4共计135000元，其中KK3成本为32000元，KK4成本为28000元，货款已经通过工行收讫。
(19)向海尚公司销售商品JP35共计182000元，成本为110000元，货款未收讫。
(20)向一通公司和兴乐公司赊购商品MT5共计205000元，KK4共计25000元，其中向一通企业购买商品140000元，向乐星公司购买商品65000元。
(21)以工行支付一通企业商品款150000元。
(22)以工行支付网络费1200元。
（四）实验步骤
凭证输入界面如下图所示：
**A、凭证界面**
1、给Excel菜单添加“开发工具”。
2、将“sheet1”工作表命名为“凭证输入”。
3、在B1输入“记账凭证”
4、选中“B1:F1”区域，点击“开始”菜单中的“对齐方式”命令里的“合并后居中”,字体设为“仿宋-GB2312”。
5、在B2中输入“凭证号：”，在D2中输入“日期：”，G2中输入“附件数：”。
6、在E2中设定单元为“2018/02/01”日期格式。
7、在A3输入“凭证ID”，B3输入“摘要”，C3:E3输入“会计科目”，C4输入“科目代码”，D4输入“总账科目”，E4输入“明细科目”，F3输入“现金流量类型”，G3输入“借方金额”，H3输入“贷方金额”，I3输入“错误提示”。
8、作一点修饰和调整，最后结果如上图所示。
9、插入“开发工具中的按钮”2个，分别为“添加纪录”、“获取最新单号”。
10、在A5单元格中输入公式：
“=IF(B5<>"",YEAR($E$2)&"-"&IF(MONTH($E$2)<10,"0"&MONTH($E$2))&"-"&IF(DAY($E$2)<10,"0"&DAY($E$2),DAY($E$2))&"-"&IF($C$2<10,"00"&$C$2,IF($C$2<100,"0"&$C$2,$C$2))&"-"&IF(ROW()-4<10,"0"&ROW()-4,ROW()-4)&"-"&IF($H$2<10,"0"&$H$2,$H$2),"")”
11、到此记账凭证就在外表上完成了，但两个控件还没有宏，所以最终还要进行处理。
12、选中C5:C18区域，点击“数据-数据验证工具-数据验证”设验证条件为“序列”，来源“=dm”出错警告中标题设为“科目代码错误”，错误信息设为“输入的科目代码不存在”
13、在D5单元格中输入公式：
=IFERROR(VLOOKUP(LEFT(C5,4)+0,kmdm,2,FALSE),"")，完成总账科目设置。
14、在E5中输入公式：
=IFERROR(VLOOKUP(C5,kmdm[#全部],2,FALSE),"")，
完成明细科目设置。
15、在I5单元格输入公式：
=IF(OR(COUNTBLANK(G5:H5)+COUNTBLANK(B5)=3,COUNTBLANK(G5:H5)+COUNTBLANK(B5)=0),"","错误")，完成错误提示的设定。（注意没有金额的单元个要输入0以便取消错误提示！）
16、选中F5:F18区域，点击“数据-数据工具-数据验证”，在“允许”中选择序列，“来源”设为“=现金流量类型”，完成数据验证。
17、选中G5:G18区域，执行“开始-样式-条件格式-新建规则”命令，在“选择规则类型”中单击“使用公式确定要设置的单元格”命令，在“为符合此公式的值设定格式”文本框中输入公式“=SUM($H$5:$H$18)<>SUM($G$5:$G$18)”，点击“格式-填充”为红色，完成条件格式的设定。
18、对G:H18区域设置会计专业格式，小数2位，不带货币符号。
19、将A5单元格公式拖动复制到A18，完成凭证ID设置。
20、将D5:E5中的公式拖曳填充到D18:E18，完成总账科目与明细科目的设置。
21、将I5的公式拖曳填充到I18，完成凭证的错误提示的设置。
22、在记账凭证中输入一笔业务，准备后续进行转入凭证库的处理工作！
23、新建一个工作表，并命名为“凭证库”。
24、在凭证库工作表中从A1单元格开始依次输入：id、凭证id、摘要、科目代码、总账科目、明细科目、现金流量类型、借金额、贷金额、类型代码、年、月、日、凭证号、笔号、附件数、性质、审核，完成凭证库各字段的输入。凭证库结构如下图所示。
25、选中A1单元格，执行“插入-表格组-表格”命令，将指定的单元格区域转化为表组（即表对象）。选中“设计”选项卡，将“属性”组内的表对象的名称更改为“pzk”，完成凭证库的设计。
26、选中B2单元格，输入字母“a”，完成凭证库首行数据的输入。（注意：这个a字母将来需要通过手工删除！）
**B、宏的录制**
（1）“添加新纪录”宏
1、选择“开发工具”
2、点击“录制宏”
3、点击H5单元格
4、点击“代码-使用相对引用”
5、同时按“ctrl+shift+向下的方向”，不要松开“ctrl+shift”，再按三下向左的方向键，将凭证的整体记录全部选上。
6、点击“ctrl+C”复制所选择的内如。
7、点击“代码-使用相对引用”，取消了相对引用。
8、选择凭证库工作表。
9、点击选择B1单元格。
10、点击“代码-使用相对引用”。
11、按“ctrl+向下的方向键”一次，再单独按一次“向下的方向键”，跳到空白行的空白单元格里。
12、右键单击拉出菜单，选择“选择性粘贴-数值”，点击“确定”。
13、点击“代码-使用相对引用”以取消相对引用。
14、选择“凭证输入”工作表，选择“B1”单元格。
15、双击该单元格（B1），退出复制模式。
16、选中B5:C18单元格区域，点击“delete”键，删除凭证摘要和科目代码，再选择“F5:H18”单元格区域，点击“Delete”，删除凭证的借方金额和贷方金额。
17、点击“开发工具-代码组-停止录制”命令，完成宏录制。
18、在“添加新纪录”按钮上右键，将刚刚录制的宏指定到“添加新纪录”按钮上，完成最后的工作。
（2）“获取最新凭单号”宏
1、选择“凭证输入”工作表，选择“开发工具-代码-录制宏”，打开“录制新宏”对话框，将宏名更改为“获取最新凭单号”，单击“确定”按钮。
2、选中C2单元格，输入公式
=MAX(IF(PZK[年]&"-"&PZK[月]=YEAR(E2)&"-"&"-"&MONTH(E2),
PZK[凭证号]),0)+1，同时按住“ctrl+shift”点击“Enter”，完成数组公式的输入。
3、点击“开发工具-代码组-停止录制”命令，完成宏的录制。
4、在按钮“获取最新凭单号”右击，执行“指定宏”命令，选择“获取最新凭单号”项，点击“确定”完成指定宏的设置。
（五）将上面业务录入凭证并转入凭证库
1、将前面输入的第一笔业务转入凭证库。
2、选中“凭证库”工作表，选中第二行（含有我们前面输入a的那一行），执行删除命令，删除第一行记录。
3、选中A2单元格，输入公式“=[@年]&"-"&[@月]&"-"&[@凭证号]&"-"&[@笔号]”，按回车键，完成ID字段的设定。
4、选中J2单元格，输入公式“=IFERROR(VLOOKUP([@现金流量类型],xjll,2,FALSE),"")”完成现金流量代码设置。
选中K2单元格，输入公式“=LEFT([@凭证ID],4)+0”，完成年字段设置。
5、选中L2单元格，输入公式“=MID([@凭证ID],6,2)+0”，完成月字段的设置。
6、选中M2单元格，输入公式“=MID([@凭证ID],9,2)+0”，完成日字段的设置。
7、选中N2单元格，输入公式“=MID([@凭证ID],12,3)+0”，完成“凭证号”字段的设置。
8、选中O2单元格，输入公式“=MID([@凭证ID],16,2)+0”，完成“笔号”字段的设置。
9、选中P2单元格，输入公式“=RIGHT([@凭证ID],2)+0”，完成“附件数”字段的设置。
10、选中Q2单元格，输入公式
“=VLOOKUP([@科目代码],kmdm,3,FALSE)”，完成“性质”字段的设置。
11、选中A2单元格，选中“视图”选项卡，执行“窗口-冻结窗口-冻结拆分窗口”命令，完成窗口冻结。
12、到此凭证库的各个字段全部设置完毕，后续可以在“凭证输入”工作表中继续完成剩余业务凭证的输入。
实验用经济业务的会计分录参考：
第1笔业务：
借：库存商品-JP34  33000
	库存商品-MT5   21000
	库存商品-KK2   8000
	应交税费-应交增值税（进项）10540
   贷：银行存款-工行  70540
第2笔业务：
借：银行存款-工行  13475
	应收账款-海尚公司7000
   贷：主营业务成本 17500（注意：应为主营业务收入）
	   应交税费-应交增值税（销项）2975
借：主营业务成本   11000
贷：库存商品-JP32  6000
	库存商品-JP33  5000
第3笔业务：
借：银行存款-工行  88000
   贷：应收账款-欧丽公司  88000
第4笔业务：
借：应付账款-一通公司250000
   贷：银行存款-工行250000
第5笔业务：
借：银行存款-工行178776
   贷：主营业务收入152800
	   应交税费-应交增值税（销项）25976
借：主营业务成本  92350
   贷：库存商品-JP35  61000
	   库存商品-MT5   28000
	   库存商品-KK2   3350
第6笔业务：
借：销售费用  98
   贷：现金  98
第7笔业务：
借：管理费用-业务招待费  5532
   贷：银行存款-工行  5532
第8笔业务：
借：应收账款-金鑫公司  297180
   贷：主营业务收入   254000
	   应交税费-应交增值税（销项）43180
借：主营业务成本   189100
	贷：库存商品-JP35 189100
第9笔业务：
借：销售费用  2200
   贷：银行存款-工行  2200
第10笔业务：
借：银行存款-工行  183924
   贷：主营业务收入  157200
	   应交税费-应交增值税（销项） 26724
借：主营业务成本   72500
   贷：库存商品-JP34  72500
第11笔业务：
借：应交税费-应交增值税（销项） 71382
   贷：银行存款-工行   71382
第12笔业务：
借：库存商品-MT4  487000
	库存商品-JP32 45000
	库存商品-JP34 102000
	应交税费-应交增值税（进项）107780
   贷：应付账款-合力企业  573300
	   应付账款-兴乐公司  168480
第13笔业务：
借：其他应付款  6022
   贷：银行存款-工行  6022
第14笔业务：
借：固定资产  35000
   贷：银行存款-工行  35000
第15笔业务：
借：其他应收款-方乐  3500
   贷：现金  3500
第16笔业务：
借：现金  175000
   贷：银行存款-工行175000
借：应付职工薪酬  175000
   贷：现金  175000
借：销售费用  95000
	管理费用-工资34500
   贷：应付职工薪酬  175000
借：销售费用  9500
	管理费用福利费  8000
   贷：应付职工薪酬   17500
第17笔业务：
借：银行存款-工行  125000
   贷：应收账款-新通讯公司125000
第18笔业务：
借：银行存款-工行  157950
   贷：主营业务收入 135000
	   应交税费-应交增值税（销项） 22950
借：主营业务成本  60000
   贷：库存商品-KK3  32000
	   库存商品-KK4  28000
第19笔业务：
借：应收账款-海尚公司  212940
   贷：主营业务收入  182000
	   应交税费-应交增值税（销项） 30940
借：主营业务成本  110000
   贷：库存商品-JP35  110000
第20笔业务：
借：库存商品-MT5  205000
	库存商品-KK4  25000
	应交税费-应交增值税（进项)  39100
   贷：应付账款-一通企业  239850
	   应付账款-兴乐公司   29250
第21笔业务：
借：应付账款-一通企业  150000
   贷：银行存款-工行  150000
第22笔业务：
借：管理费用-其他  1200
   贷：银行存款-工行1200
第23笔业务：
借：管理费用-劳保费用  8000
   贷：银行存款-工行  8000
**三、凭证编辑**
（一）实验目的
掌握凭证修改的原理，了解凭证修改的设计思路。
（二）实验要求
了解高级筛选方法，了解宏的录制。
（三）实验数据
（1）调出凭证库中1月第2号凭证。
（2）修改凭证工作表中第2号凭证，将主营业务支出更改为主营业务收入。
（四）实验步骤
凭证编辑界面如下图所示：
**A、设定查询条件**
1、新建一个工作表，并命名为“凭证编辑”。
2、从A1:A2单元格合并，开始输入“凭证修改/查询条件：”，B1单元格输入“年”，C1单元格输入“月”，D1单元格输入“凭证号”，完成后如表所示。
3、选中“开发工具-控件-插入-表单控件-按钮”，放置在E1单元格，命名按钮名为“调用凭证”，同样增加另外两个按钮，分别是“确认修改”和“删除凭证”。
**B、设置凭证修改界面**
1、选中C4:G4单元格区域，执行“开始-对齐方式-合并居中”，输入“记账凭证”，设定字体为仿宋-GB2312，字号20磅，将C4:G4单元格区域设置为双下画线。
2、选中B5单元格，输入“凭证号“，C5单元格输入公式“=D2”,完成凭证号的设计。
3、选中D5单元格，输入“日期”，选中E5单元格，输入公式“=LEFT(A8,10)+0”，完成凭证输入日期的设置。
4、选中G5单元格，输入“附件数”，在H5单元格输入公式“=RIGHT(A8,2)+0”，完成凭证附件数的设置。
5、A6：A7单元格区域合并，输入“凭证ID”，B6:B7合并，输入“摘要”，C6:E6合并居中输入“会计科目”，C7输入“科目代码”，D7输入“总账科目”，E7输入“明细科目”。
6、F6:F7合并，输入“现金流量类型”，G6:G7合并输入“借方金额”，H6:H7合并输入“贷方金额”，I6:I7合并输入“错误提示”，完成其他格式的设计。
7、在D8单元格输入公式：
“=IFERROR(VLOOKUP(LEFT(C8,4)+0,kmdm,2,FALSE),"")”，完成总账科目名称的设置。
8、在E8单元格输入公式：
“=IFERROR(IF(LEN(C8)=6,VLOOKUP(C8,kmdm,2,FALSE),""),"科目代码有误")”，完成明细科目名称的设置。
9、选中D8:E8区域，托拽复制到D22:E22单元格区域，完成凭证其余行的公式设置。
10、在I8单元格输入公式：
“=IFERROR(IF(LEN(C8)=6,VLOOKUP(C8,kmdm,2,FALSE),""),"科目代码有误")”，托拽复制到I22单元格，完成错误提示的设置。
11、选中F8:F22区域，执行“数据-数据工具-数据验证”命令，“允许A”设为“序列”，“来源S”设为“=现金流量类型”，完成数据有效性设置。
12、选中G8:G22单元格区域，执行“开始-样式-条件格式-新建规则”命令，在“选择规则类型”中选中“使用公式确定要设置格式的单元格”命令，打开“新建格式规则”对话框，在“为符合此公式的值设置格式”下的文本框内输入公式：
“=SUM($H$8:$H$22)<>SUM($G$8:$G$22)”，单击“格式”按钮，选择“填充”，选择红色，点击“确定”，完成条件格式设置。
13、选中G8:H22区域，执行“设置单元格格式”命令，设置“数字类型-会计专用”，小数位为2，不显示货币符号，完成借贷金额的显示设置。
**C、调用需要修改的凭证**
1、在“凭证编辑”工作表中，B2输入“2018”，C2输入“1”，D2中输入“2”，完成高级筛选条件的设定。
2、选择“开发工具-代码-录制宏”命令，打开“录制新宏”对话框，将宏名改为“调出凭证”，单击“确定”按钮，开始录制宏。
3、在“凭证编辑”工作表中选择A8:C22区域，点击“Delete”，删除指定单元格区域的内容。选中F8:H22单元格区域，点击“Delete”删除指定区域内容。
4、在“凭证编辑”工作表中，选中A8单元格，选中“凭证库”工作表，选中“数据-排序和筛选-高级”命令，打开“高级筛选”对话框，在列表区域输入“=pzk[#全部]”，单击条件区域的折叠按钮，选择“凭证编辑”工作表中的B1~D2单元格区域，点击“确定”按钮，完成高级筛选的操作。
5、同时按下“Ctrl+G”键，打开定位对话框，在引用位置中输入“=pzk[[#数据],[凭证ID]:[科目代码]]”,单击“确定”，定位到指定位置。
6、同时按“Ctrl+C”键复制内容，选中“凭证编辑”工作表中的A8单元格，右键执行“选择性粘贴-粘贴数值”命令，将凭证ID、摘要和科目代码复制到制定的位置。
7、选中“凭证库”工作表，在编辑栏内单击一下，推出复制状态，选中A1单元格，同时按“Ctrl+G”，打开定位对话框，在引用位置中输入“=pzk[[#数据],[现金流量类型]:[贷金额]]”,点击“确定”，定位到指定位置。同时按“Ctrl+C”复制内容，然后选中“凭证编辑”工作表中的F8单元格，右键执行“选择性粘贴-粘贴数值”命令，最终将借方金额与贷方金额复制到指定位置。
8、选中“凭证库”工作表，选择“数据-排序和筛选-清除”命令，清除高级筛选的结果，然后选择“凭证编辑”工作表。
9、选中“凭证编辑”工作表，点击“开发工具-代码组-停止录制”命令，完成宏的录制。
10、在“调用凭证”按钮上右击，执行“指定宏”命令，打开“指定宏”对话框，选择“调出凭证”项，点击“确定”，完成指定宏的设置。
**D、修改凭证**
1、选择“凭证编辑”工作表，在B2单元格输入2018，C2单元格输入数值1，D2单元格输入数值2，点击“调出凭证”按钮，完成高级筛选条件的设定。
2、在“凭证编辑”工作表中执行“开发工具-代码-录制宏”命令，打开“录制新宏”对话框，将宏名称更改为“确认修改”，单击“确定”按钮，开始录制宏。
3、选中“凭证编辑”工作表的A8单元格，然后选中“凭证库”工作表，选中“数据-排序和筛选-高级”命令，打开“高级筛选”对话框，在列表区域输入“pzk[#全部]”，点击条件区域后的折叠按钮，选中“凭证编辑”工作表中的B1~D2单元格区域，点击“确定”按钮，完成高级筛选的操作。
4、同时按“Ctrl+G”打开定位对话框，在引用位置中输入“=pzk[#数据]”，点击“确定”按钮，定位到指定的位置。
5、同时按下“Shift+Ctrl+向右方向键”，在选中的位置右击，执行“删除行”命令，删除指定的记录。
6、选中“数据-排序和筛选-清除”命令，退出筛选过程。
7、选中“凭证编辑”工作表中的H8单元格，然后在“开发工具”选项卡的代码组内选择“使用相对引用”命令，进入相对模式。
8、同时按“Shift+Ctrl+向下方向”键，再按“Shift+Ctrl+向左方向”键，按3次向左方向键，再按“Ctrl-C”，完成要复制内容的选择复制操作。
9、在“开发工具”选项卡的代码组内，取消选择“使用相对引用”命令，进入绝对模式。
10、选择“凭证库”工作表中的B2单元格，然后在“开发工具”选项卡的代码组内选择“使用相对引用”命令，同时按“Ctrl+向下方向键”，再单独按一次向下方向键，这样会定位到空白单元格内，在空白单元格内右击，执行“选择性粘贴-数值”，点击“确定”按钮，完成数据的粘贴操作。
11、在“开发工具”选项卡代码组内，取消“使用相对引用”命令，进入绝对引用模式。
12、选中“凭证库”工作表中的B1单元格，双击该单元格，退出复制模式，然后选中C1单元格，选择“凭证编辑”工作表，选中“A8:C22”单元格区域，点击“Delete”删除凭证摘要和科目代码，然后选中“F8:H22”单元格区域，点击“Delete”，删除凭证的现金流量类型、借方金额和贷方金额，删除已经导入凭证的数据。
13、单击“开发工具-代码组-停止录制”命令，完成宏的录制。
14、在“确认修改”按钮上右击，执行“指定宏”命令，打开“指定宏”对话框，选择“确认修改”项，单击“确定”，完成指定宏的设置。
**四、凭证查询审核与打印**
（一）实验目的
掌握凭证提取的方法，设计辅助列的方法。
（二）实验要求
能熟练操作高级筛选方法，了解宏的录制。
（三）实验内容及数据来源
要求从凭证库中调出指定的凭证，并显示如下字段：
（1）凭证号：指定年月的某笔凭证。
（2）记账日期：提取“凭证库”中“id”字段的值。
（3）附件数：一张凭证号中每笔记录的附件数都是相同的，因此只需要提取“凭证库”中对应凭证号第一笔分录的附件数。
（4）摘要：提取“凭证库”中“摘要”字段的值。
（5）总账科目：提取“凭证库”中对应“总账科目”字段的值。
（6）明细科目：提取“凭证库”中对应“明细科目”字段的值。
（7）借方金额：提取“凭证库”中对应“借金额”字段的值。
（8）贷方金额：提取“凭证库”中对应“贷金额”字段的值。
（9）合计：如果凭证大于4笔分录，那么凭证将会被分页，并且合计数只在最后一页中显示，如果凭证数小于4笔分录，凭证就只有一页，直接对借方金额和贷方金额进行汇总。
**提取完成后，在本实验完成下述任务：**
（1） 通过输入指定的年、月和凭证号信息，审核第一、二张凭证。
（2）打印第2号凭证。
（四）实验步骤
凭证审核与打印界面如下图所示：
**A、凭证审核条件设定**
1、新建一个工作表，命名为“凭证审核与打印”。
2、选择A1:B2单元格区域，执行“开始-对齐方式-合并后居中”，输入“凭证审核/打印条件”。
3、在C1输入“年”，D1输入“月”，E1输入“凭证号”，F1输入“总页数”，G1输入“当前页”后，作简单修饰。
4、选中“开发工具-控件-插入-表单控件-数值调节”按钮，在E2单元格内拖曳出一个“数值调节”按钮，完成按钮的放置操作。
5、在E2单元格的“数值调节”按钮上右击，执行“设置控件格式”命令，打开“设置控件格式”对话框，点击“控制”标签，“当前值”选择为1，“最小值”设置为1，“步长”设为1，单击单元格链接后的折叠按钮，选择E2单元格，点击“确定”，完成对E2单元格“数值调节”按钮的设置。
6、同样在G2单元格上也放置一个“数值调节”按钮，并设“当前值”为1，“最小值”为1，“步长”为1，完成G2单元格“数值调节”按钮的设置。
**B、凭证区域设定**
1、在“凭证审核与打印”工作表中，从第4行开始，输入记账凭证的各个项目。
2、选中“开发工具-控件-插入-命令按钮”，插入两个命令按钮，“凭证审核”和“打印凭证”。
3、选中F2单元格输入公式“=ROUNDUP(COUNTIF(PZK[ID],$C$2&"-"&$D$2&"-"&$E$2&"-*")/4,0)”，* 完成凭证显示总页数的设置。（注意：这里就是我们运用ID号的作用！，函数COUNTIF可以接受通配符号\*的，除以4的目的就是超过4行就有下一页）
4、点击A8单元格，在编辑栏内输入公式“=$C$2&"-"&$D$2&"-"&$E$2&"-"&(ROW()-7)+($G$2-1)*4”，* 完成第一行ID的设定。最后将A8公式复制到A9:A11区域，完成整个ID的设定。
5、选中C5单元格，在编辑栏输入公式“=E2”，完成凭证号的设置。
6、选中E5单元格，在编辑栏内输入公式“=LEFT(VLOOKUP(C2&"-"&D2&"-"&E2&"-1",PZK,2,FALSE),10)”，完成日期的设置。(注意：凭证ID是由：年-月-日-凭证号-笔号-附件数构成的)
7、选中G5单元格，在编辑栏内输入公式“=VLOOKUP(C2&"-"&D2&"-"&E2&"-1",PZK,16,FALSE)”，完成附件数的确认。
8、选中B8单元格，在编辑栏输入公式：
“=IFERROR(VLOOKUP(A8,PZK,3,FALSE),"")”，将公式拖曳复制到B11，完成摘要的提取。
9、选中C8单元格，输入公式：
“=IFERROR(VLOOKUP(A8,PZK,4,FALSE),"")”，将C8公式拖曳复制到C11，完成科目代码的设置。
10、选中D8单元格，输入公式：
“=IFERROR(VLOOKUP(A8,PZK,5,FALSE),"")”，将公式拖曳复制到D11，完成总账科目的设置。
11、选中E8单元格，输入公式：
“=IFERROR(VLOOKUP(A8,PZK,6,FALSE),"")”，将公式拖曳复制到E11，完成明细科目的设置。
12、选中F8单元格，输入公式：
“=IFERROR(VLOOKUP(A8,PZK,8,FALSE),0)”，将公式拖曳复制到F11，完成借方金额的提取设置。
13、选中G8单元格，输入公式“”，将公式拖曳复制到G11，完成贷方金额的提取设置。
14、选中H8单元格，输入公式：
“=IF(COUNTIFS(PZK[ID],A8,PZK[审核],"已审核")=1,"√","")”，将公式拖曳复制到H11，完成审核信息的提取设置。
15、选中F12单元格，输入公式：
“=IF($F$2=$G$2,SUMIF(pzk[ID],$C$2&"-"&$D$2&"-"&$E$2&"-*",pzk[借金额]),"")”，* 完成凭证借方合计的计算设置。
16、选中G12单元格，输入公式：
“=IF($F$2=$G$2,SUMIF(pzk[ID],$C$2&"-"&$D$2&"-"&$E$2&"-*",pzk[贷金额]),"")”，* 完成凭证贷方合计的计算设置。
**C、设置条件格式**
1、选中F2:G2单元格区域，执行“开始-样式-条件格式-新建规则”命令，在打开的对话框中选择“使用公式确定要设置格式的单元格”命令，再打开“新建格式规则”对话框，在“为符合此公式的值设置格式”中设定公式为“=$G$2>$F$2”。
2、接上面的设置，点击“格式”按钮，选择“填充”选项卡，选择红色填充，点击“确定”，完成F2:G2条件样式的设置。
3、选中F12:G12单元格区域，执行“开始-样式-条件格式-新建规则”命令，选择“使用公式确定要设置格式的单元格”命令，打开“新建格式规则”对话框，在“为符合此公式的值设置格式”中设定公式“=$G$12<>$F$12”。单击对话框中的“格式”按钮，选择“填充”选项卡，选择红色作为填充色，点击“确定”，完成F12:G12条件样式的设置。
**D、审核凭证**
1、选择“开发工具-代码-录制宏”命令，打开“录制新宏”对话框，将宏名更改为“凭证审核”，点击“确定”开始录制新宏。
2、选中“凭证库”工作表，选中“数据-排序和筛选-高级”命令，打开“高级筛选”对话框，在列表区域输入“pzk[#全部]”，点击条件区域后的折叠按钮，选中“凭证审核与打印”工作表中的C1:E2单元格区域，点击“确定”按钮，完成高级筛选操作。
3、同时按“Ctrl+G”键，打开“定位”对话框，在“引用位置”中输入公式“=pzk[[#数据],[审核]]”,单击“确定”按钮，定位到指定位置。
4、输入“已审核”，同时按“Ctrl+Enter”，将选中的单元格区域都填充同样的内容。
5、选中A1单元格，执行“数据-排序和筛选-清除”命令，再次选中“凭证审核与打印”工作表。
6、在“凭证审核与打印”工作表中，执行“开发工具-代码-停止录制”命令，完成宏的录制。
7、在“凭证审核”按钮上右击，执行“指定宏”命令，打开“指定宏”对话框，选择“凭证审核”项，点击“确定”按钮，完成指定宏的设置。
**审核练习：经过以上的处理我们就可以对每一张凭证进行审核处理。**
1、在C2单元格输入2018，D2输入数值1，E2输入数值1，在G2单元格输入数值1，完成审核条件的设置
2、点击“凭证审核”按钮，完成审核操作。审核以后就会在审核栏中出现“√”标志，“凭证库”相应的记录的审核字段也会出现“已审核”字样。
3、点击G2单元格上的数值调节按钮，可以看到一笔经济业务的所有记录均已审核完成。
4、点击E2单元格上的数值调节按钮，就可以对所有凭证的审核情况进行查询并完成对应的审核工作。
**E、打印凭证**
1、执行“开发工具-代码-录制宏”命令，打开“录制新宏”对话框，将宏名更改为“打印凭证”，点击“确定”按钮，开始录制新宏。
2、在“凭证审核与打印”工作表中选择B4单元格，按住“Shift”键不放，再次选中H13单元格，完成打印区域的选择。
3、选中“页面布局”选项卡，执行“页面设置-设置打印区域”命令，完成打印区域的设定。
4、点击“文件”按钮，执行“打印-快速打印”命令，完成打印操作。
5、点击“凭证审核与打印”工作表中的B4单元格，执行“开发工具-代码-停止录制”命令，完成宏的录制。
6、在“凭证打印”按钮上右击，执行“指定宏”命令，打开“指定宏”对话框，选择“打印凭证”项，单击“确定”，完成指定宏的设置。
**F、公式的保护**
1、单击工作表左上角交叉点（或Ctrl+A）将工作表全部选定。
2、单击右键，执行“设置单元格格式”命令，打开“设置单元格格式”对话框，点击“保护”标签，将“锁定”前的勾选去除，单击“确定”按钮，解除对所有单元格的锁定。
3、按住“Ctrl”键的同时，选中工作表中所有使用公式的单元格，完成要保护的单元格的选定。
4、在选中的任意一个单元格上右击，执行“设置单元格格式”命令，打开“设置单元格格式”对话框，点击“保护”标签，将“锁定”勾选，单击“确定”按钮，完成对单元格的锁定。
5、单击“开始”选项卡，选择“单元格”组内的“格式”，执行“保护”下的“保护工作表”命令，打开“保护工作表”对话框，输入解除保护的密码“12345”，点击“确定”按钮，打开“确认密码”对话框，再次输入“12345”，点击“确定”完成对工作表的保护设定。
6、同样的道理，我们可以对前面的工作表进行合理保护。（大家自己做做看！）
**五、科目汇总表**
（一）实验目的
熟悉科目汇总表的结构和Microsoft Query提取数据的方法。
（二）实验要求
了解Microsoft Query的使用方法。
（三）实验内容及数据来源
科目汇总表包括的内容：
（1）日期信息。
（2）科目代码。
（3）科目名称。
（4）指定月借方发生额。
（5）指定月贷方发生额。
（四）实验步骤
**A、科目汇总表界面**
科目汇总表界面如下图所示：
1、新建一个工作表，将其命名为“科目汇总表”。
2、在A2单元格输入“年份”，D2单元格输入“月份”。
3、选中A1:D1单元格区域，执行“开始-对齐方式-合并居中”命令，并输入公式“=B2&"年"&D2&"月科目汇总表"”，作一点修饰，完成表头的设计。
4、在A3、B3、C3、D3单元格分别输入“科目代码”、“总账科目名称”、“借方发生额”、“贷方发生额”，让各个项目居中。
**B、科目汇总表数据**
1、选中A4单元格，执行“数据-获取外部数据-自其他来源-来自Microsoft Query”命令，打开“选择数据源”。
2、在“选择数据源”对话框中选择数据库类型为“Excel Files”，单击“确定”按钮，打开“选择工作簿”对话框。
3、在驱动器中选择数据库，名称就指定为“账务处理.xlsm”，点击“确定”按钮，打开“查询向导-选择列”对话框，单击“可用的表和列-凭证库”前的“+”选中要显示的列为“科目代码、总账科目、借金额和贷金额”。（注意：当改变账务处理.xlsm的路径时，就需要重新选择数据库）
4、点击“下一页”按钮，完成要显示的列的选择，进入“查询向导-筛选数据”对话框。
5、点击“下一页”按钮，逃过筛选过程，打开“查询向导-排序顺序”的对话框，在这个步骤中不要设置任何内容。
6、点击“下一页”按钮，打开“查询向导-完成”对话框，在“请确定下一步的动作”中，选择“在Microsoft Query中查看数据或编辑查询”，点击“完成”按钮，进入Microsoft Query窗口。
7、在Microsoft Query窗口中，选中“科目代码”列中任意一个数据，执行“记录-编辑列”命令，打开“编辑列”对话框，将“字段”下的内容更改为“Left(科目代码,4)”，单击“确定”按钮，完成列的设置。
8、在Microsoft Query窗口中，选中“借金额”列中任意一个数据，执行“记录-编辑列”命令，打开“编辑列”对话框，将“字段”下拉列表中选择“借金额”，在“总计”下拉列表中选择“求和”，单击“确定”按钮，完成借金额字段的求和设置。
9、在Microsoft Query窗口中，选中“贷金额”列中任意一个数据，执行“记录-编辑列”命令，打开“编辑列”对话框，将“字段”下拉列表中选择“贷金额”，在“总计”下拉列表中选择“求和”，单击“确定”按钮，完成贷金额字段的求和设置。
10、在Microsoft Query窗口中，执行“条件-添加条件”命令，打开“添加条件”对话框，保持“总计”下拉列表为空，选择“字段”为“年”，“运算符”设置为“等于”，“指定值”设置为“[nian]”，单击“添加”按钮，完成年份条件的设置。（注意：nian是个变量，两个条件之间的关系是“与”的关系）
11、保持“总计”下拉列表为空，选择“字段”为“月”，“运算符”设置为“等于”，“指定值”设置为“[yue]”，单击“添加”按钮，完成月份条件的设置。（注意：yue是个变量，两个条件之间的关系是“与”的关系）
12、在Microsoft Query窗口中执行“文件-将数据返回Microsoft Excel”，打开“导入数据”对话框，将数据放置的位置设置为“科目汇总表”的A4单元格（=$A$4），完成数据导入起始位置的设定。
13、单击“导入数据”对话框中的“确定”按钮，打开“输入参数值”对话框，单击“nian”下的文本框后的折叠按钮，单击工作表中的B2单元格，并且勾选“在以后的刷新中使用该值或该引用”和“当单元格值更改时自动刷新”复选框，单击“确定”按钮，完成年份参数的设置。同理也要完成月份参数的设置。（nian参数：“=科目汇总表!$B$2”，yue参数：“=科目汇总表!$D$2”）
14、选中A4单元格，选择“设计-表样式”中的第一种样式“无”，勾选“表样式选项”组中的“汇总行”项，添加一个汇总行，将借金额和贷金额的汇总方式都设置为“求和”。
15、选中A3:D19单元格区域，设置指定的单元格区域包含边框线。
16、选中C5:D19单元格区域，右击执行“设置单元格格式”命令，选择“数值类型”为“会计专用”，不显示“货币符号”类型。
17、选中A4单元格，选择“设计”选项卡，执行“外部表数据”组中的“属性”命令，打开“外部数据属性”对话框，取消“调整列宽”复选框不被选中，单击“确定”按钮，完成格式和布局的设置。
（注意：当汇总行的“借方发生额”和“贷方发生额”两个数据一致时，说明所有的凭证借方贷方金额没有发生输入错误，从而达到试算平衡。当要查看其他月份的时候，只要修改D2单元格的值即可。以上的过程在不同版本的Office Excel下是不一样的，请大家自己根据情况进行练习。）
**六、科目余额表**
（一）实验目的
掌握科目余额表的结构和Microsoft Query提取数据的方法。
（二）实验要求
了解Microsoft Query的用法。
（三）实验内容及数据来源
科目余额表需要实现的功能包括：
（1）期初各个会计科目的借方和贷方余额。
（2）当期各个会计科目的借方和贷方余额。
（3）计算本期期末各个会计科目的借方和贷方余额。
（三）实验步骤
**A、科目余额表界面**
科目余额表界面如下图所示：
1、新建工作表，将工作表命名为“科目余额表”。
2、选中E1:G1单元格区域，执行“开始-对齐方式-合并居中”命令，设置字体“仿宋_GB2312”，字号为24磅，并设置双下画线，完成表头设置。
3、选中E1单元格，在编辑栏内输入公式“=B2&"年"&G2&"月科目余额表"”，完成标题的设置。
4、从第2行开始输入表头中的相关各项表名称。
**B、导入科目代码**
1、选中“数据”选项卡，在“获取外部数据”组中，执行“自其他来源”中的“来自Microsoft Query”命令，打开“选择数据源”对话框。
2、在“选择数据源”对话框中，选择“数据库类型”为“Excel Files*”，单击“确定”按钮，打开“选择工作簿”对话框。
3、选择工作簿所在的文件路径，单击“确定”按钮，打开“查询向导-选择列”对话框，单击“科目代码表”前的“+”好，展开该表所包含的字段，选中要显示的列为“科目代码”、“科目名称”、“性质”和“年初数”。
4、单击“下一步”按钮，完成要显示的列的选择，进入“查询向导-筛选数据”对话框，点击“下一步”按钮，跳过筛选过程，打开“查询向导-排序”对话框，不做任何设置，点击“下一步”按钮，打开“查询向导-完成”对话框，在“请确定下一步的动作”选中“在Microsoft Query中查看数据或编辑查询”，单击“完成”按钮，完成查询向导的操作，进入Microsoft Query界面。
5、在“Microsoft Query”窗口中，执行“条件-添加条件”命令，打开“添加条件”对话框，选择“字段”为“是否明细”，“指定值”为“y”，单击“添加”按钮，完成条件设置，点击“关闭”按钮。
6、执行Microsoft Query窗口中文件菜单下的“将数据返回Microsoft Excel”中，打开“导入数据”对话框，将“数据的放置位置”设置为“科目余额表”的A5单元格，完成数据导入的操作。
7、选中A5单元格，选中“设计”选项卡，在“表样式”组中选择一种样式。选中D5单元格，右击，在打开的菜单中执行“插入”下的“在右侧插入表列”命令，插入一个新列。使用同样的方法插入其他5列，完成表主体结构的设置。
8、选中A5单元格，打开“数据”选项卡，执行“排序和筛选”组内的“筛选”命令，退出筛选状态。
9、选中A5单元格，选中“设计”选项卡，将“属性”组内的表名称更改为“kmye”，完成表名称的设置。
**C、科目余额**
这里科目余额需要通过计算获得，请大家认真思考。
1、选中E6单元格，在编辑栏内输入公式“=IF([@性质]=1,[@性质]*[@年初数]+(SUMIFS(PZK[借金额],PZK[科目代码],[@科目代码],PZK[月],"<"&$G$2)-SUMIFS(PZK[贷金额],PZK[科目代码],[@科目代码],PZK[月],"<"&$G$2)),0)”，完成期初余额的计算。
2、选中F6单元格，在编辑栏内输入公式“=IF([@性质]=-1,ABS([@性质]*[@年初数]+(SUMIFS(PZK[贷金额],PZK[科目代码],[@科目代码],PZK[月],"<"&$G$2)-SUMIFS(PZK[借金额],PZK[科目代码],[@科目代码],PZK[月],"<"&$G$2))),0)”，完成期初贷方余额的计算。
3、选中G6单元格，在编辑栏输入公式“=SUMIFS(PZK[借金额],PZK[科目代码],[@科目代码],PZK[月],$G$2)”
4、选中H6单元格，在编辑栏输入公式“=SUMIFS(PZK[贷金额],PZK[科目代码],[@科目代码],PZK[月],$G$2)”
5、选中I6单元格，在编辑栏输入公式“=IF([@性质]=1,[@列1]+[@列3]-[@列4],0)”，完成借方余额的计算。
6、选中J6单元格，在编辑栏输入公式“=IF([@性质]=-1,[@列2]+[@列4]-[@列3],0)”，完成贷方余额的计算。
7、选中C6:J79单元格区域，右击，执行“设置单元格格式”命令，选择数据类型为“会计专用”，不显示货币符号类型。
8、选中“科目余额表”的A5单元格，选中“设计”选项卡，执行“表格样式选项”组内的“汇总行”命令，完成添加“汇总行”的命令。
9、在“汇总”行中，选中列5所在的列，从下拉列表中选择汇总方式为“求和”，完成汇总期末借方余额的计算。
10、用同样的方法，将汇总行中列1、列2、列3、列4字段所在的列汇总方式设为“求和”。
11、在“汇总”行中，选中年初数所在的列，在编辑栏内输入公式“=SUM([性质]*[年初数])”，* 同时按“Shift+Ctrl+Enter”键，完成数组公式的输入
12、选中第5行，即字段名称所在的行，右击，执行“隐藏”命令，隐藏指定行。
13、选中C列和D列，右击执行“隐藏”命令，隐藏指定的列。
14、选中A6单元格，选择“设计”选项卡，执行“外部表数据”组内的“属性”命令，打开“外部数据属性”对话框，在“数据格式和布局”下不要选中“调整列宽”项，单击“确定”按钮，完成外部数据属性的设置。
15、对科目余额表作一下修饰。
**D、获取最新数据**
在这里我们仍然用“宏”来完成数据的刷新。
1、选中“科目余额表”，执行“开发工具-控件-表单控件-按钮控件”命令，在J1单元格内拖出一个命令按钮，将按钮名字更改为“刷新数据”，完成命令按钮的放置。
2、选择“开发工具-代码-录制宏”命令，打开“录制宏”对话框，将宏名称更改为“刷新科目”，单击“确定”按钮，开始录制宏。
3、选中A6单元格，右击，执行“刷新”命令。单击“开发工具-代码组-停止录制”命令，完成宏的录制。
4、在“刷新数据”按钮上右击，执行“指定宏”命令，打开“指定宏”对话框，选择“刷新科目”项，单击“确定”按钮，完成指定宏的设置。
现在可以试着在科目代码表中添加一个科目，例如“代码：122104会计科目：你的名字”，然后点击“科目余额表”中的“刷新数据”按钮，看看结果的变化。
**七、编制总分类账**
（一）实验目的
掌握总分类账的结构。
（二）实验要求
了解数据验证的方法和带条件的计算求和方法。
（三）实验内容及数据来源
要求根据已经完成的科目余额表中的期初数和凭证库中的凭证数据编制总分类账。
（四）实验步骤
**A、总分类账界面**
总分类账界面如下图所示：
1、新建一个工作表，将表名更改为“总分类账”，选中D1:E1单元格区域，执行“开始-对齐方式-合并居中”，设置字号为24磅，字体为“仿宋_GB2312”，在D1单元格输入“总分类账”，完成总分类账标题的设置。
2、从A2单元格开始输入“年份”、2018、“月份”、1。如图所示。
3、选中C3单元格，选中“数据”选项卡，单击“数据工具”组中的“数据验证”按钮，打开“数据验证”对话框，在“允许”下拉列表中选择“序列”，在“来源”下的文本框内输入“=dm”,单击“确定”按钮，完成数据验证设置。
4、选中F3单元格，在编辑栏输入公式：
“=IF(LEN(C3)=4,VLOOKUP($C$3,kmdm,2,FALSE),(VLOOKUP(LEFT($C$3,4)+0,kmdm,2,FALSE))&"-"&VLOOKUP($C$3,kmdm,2,FALSE))”，完成会计科目名称的设置。
**B、总分类账数据**
1、选中C2单元格，在编辑栏内输入公式“=科目余额表!B2”，选中F2单元格，在编辑栏内输入公式“=科目余额表!G2”，选中A4单元格，在编辑栏内输入公式“=C2&"年"”，完成年份和月份信息的设置。
2、选中A6单元格，在编辑栏内输入公式“=$F$2”,按回车确认，然后将A6单元格的内容向下拖曳到A7和A8单元格，完成月份信息的填制。
3、在B6单元格内输入1，选中B7单元格，在编辑栏内输入公式“=DAY(DATE($C$2,$F$2+1,0))”,计算指定月份的天数，选中B8单元格，在编辑栏内输入公式“=B7”，完成日信息的填制。
4、在C6单元格内输入“期初余额”，在C7单元格输入“本月发生额”，C8单元格输入“本月合计”，完成摘要内容的设定。
5、选中D6单元格，在编辑栏内输入公式：
“=IF(LEN(C3)=6,SUMIFS(kmye[列1],kmye[科目代码],C3),
SUM(IF(LEFT(kmye[科目代码],4)+0=LEFT($C$3,4)+0,kmye[列1],0)))”,同时按“Ctrl+Shift+Enter”键，完成借金额期初数的设置。
6、选中E6单元格，在编辑栏内输入公式：
“=IF(LEN(C3)=6,SUMIFS(kmye[列2],kmye[科目代码],
C3),SUM(IF(LEFT(kmye[科目代码],4)+0=LEFT($C$3,4)+0,kmye[列2],0)))”，同时按“Ctrl+Shift+Enter”键，完成贷金额期初数的设定。
7、选择F6单元格，在编辑栏内输入公式“”，完成借贷方向的设定。
8、选中G6单元格，在编辑栏内输入公式“”，完成期初余额的设置。
9、选中D7单元格，在编辑栏内输入公式：
“=IF(LEN(C3)=6,SUMIFS(kmye[列3],kmye[科目代码],
C3),SUM(IF(LEFT(kmye[科目代码],4)+0=LEFT($C$3,4)+0,kmye[列3],0)))”，同时按“Ctrl+Shift+Enter”键，完成指定月份借方发生额的设置。
10、选中E7单元格，在编辑栏内输入公式：
“=IF(LEN(C3)=6,SUMIFS(kmye[列4],kmye[科目代码],C3),
SUM(IF(LEFT(kmye[科目代码],4)+0=LEFT($C$3,4)+0,kmye[列4],0)))”，同时按“Ctrl+Shift+Enter”键，完成指定月份贷方发生额的设置。
11、选中G7单元格，在编辑栏内输入公式“=IF($F$6="借",D7-E7,E7-D7)”完成当月借方发生额差额的计算。
12、选中F7单元格，在编辑栏内输入公式“=IF(AND($F$6="借",G7<0),"贷",IF(AND($F$6="贷",G7<0),"借",IF(G7=0,"平",$F$6)))”，完成借贷方向的设定。
13、选中D8单元格，在编辑栏内输入公式“=SUM(D6:D7)”，完成本月借方合计数的计算。
14、选中E8单元格，在编辑栏内输入公式“=SUM(E6:E7)”，完成本月贷方合计数的计算。
15、选中G8单元格，在编辑栏内输入公式“=IF($F$6="借",D8-E8,E8-D8)”，完成指定科目当月余额的计算。
16、选中F8单元格，在编辑栏内输入公式“=IF(AND($F$6="借",G8<0),"贷",IF(AND($F$6="贷",G8<0),"借",IF(G8=0,"平",$F$6)))”，完成指定科目余额借贷方向的设置。
**C、作优化显示**
1、选中G7:G8单元格区域，右击，执行“设置单元格格式”命令，打开“设置单元格格式”对话框，选择“数字”选项卡，在分类中选择“自定义”，在类型中输入“#,##0.00;#,##0.00”,单击“确定”按钮，完成数值显示方式的设定。
2、选中整张工作表，右击，执行“设置单元格格式”命令，选中“保护”选项卡，保持“锁定”被勾选，单击“确定”按钮，完成全部单元格的锁定。
3、选中C3单元格，右击，执行“设置单元格格式”命令，选中“保护”选项卡，保持“锁定”不被勾选，单击“确定”按钮，完成可编辑单元格的指定操作。
4、单击“开始”选项卡，执行“单元格-格式-保护工作表”命令，打开“保护工作表”对话框，设置一个合适的密码，完成工作表的保护操作。
**八、明细分类账**
（一）实验目的
掌握利用明细分类账的设计方法。
（二）实验要求
了解Microsoft Query的用法，掌握宏的录制和指定。
（三）实验内容及数据来源
明细分类账要实现如下几个功能：
（1）列出指定月份和科目代码的期初数。
（2）列出本期指定会计科目的发生记录，并将这些记录填到表中。
（3）计算本期期末的借方和贷方余额。
（四）实验步骤
**A、明细分类账的界面**
明细分类账的界面如下图所示：
1、选中A1:H1单元格区域，执行“开始-对齐方式-合并居中”命令，选择字号24磅，在编辑栏内输入公式“=F3&"明细分类账"”，完成明细分类账标题的设置。
2、选中C3单元格，选中“数据”选项卡，单击“数据工具-数据验证”按钮，在“数据验证”对话框中，将“允许”下拉列表中选择“序列”，在“来源”下的文本框内输入“=dm”，单击“确定”按钮，完成数据有效性设置。
3、选中F3单元格，在编辑栏内输入公式：
“=IF(LEN(C3)=4,VLOOKUP($C$3,kmdm,2,FALSE),(VLOOKUP(LEFT($C$3,4)+0,kmdm,2,FALSE))&"-"&VLOOKUP($C$3,kmdm,2,FALSE))”
4、选中F2单元格，在编辑栏内输入“=科目余额表!G2”，选中A6单元格，在编辑栏内输入公式“F2”，完成月份的设置。
5、在B6单元格中输入数值1，在D6单元格内，输入“期初余额”。
6、选中E6单元格，在编辑栏内输入公式“=SUMIF(kmye[科目代码],$C$3,kmye[列1])”,完成借方金额的设定。
7、选中F6单元格，在编辑栏内输入公式“”，完成期初贷方金额的设置。
8、选中G6单元格，在编辑栏内输入公式“=IF(SUMIF(kmdm[科目代码],$C$3,kmdm[性质])>0,"借","贷")”，完成借贷方向的设置。
9、选中H6单元格，在编辑栏内输入公式“=IF(G6="借",E6-F6,F6-E6)”，完成期初余额的设置。
**B、导入业务信息**
明细分类账中显示的内容和金额是由凭证库中导入的，导入指定月份的业务信息的操作步骤如下：
1、选中“数据”选项卡，在“获取外部数据”组中执行“自其他来源”中的，“来自Microsoft Query”命令，打开“选择数据源”对话框。
2、在“选择数据源”对话框中选择数据库类型为“Excel Files*”，单击“确定”按钮，打开“选择工作簿”对话框。
3、选择本工作薄所在的路径，单击“确定”按钮，打开“查询向导-选择列”对话框，单击“可用的表和列”中“凭证库”前的“+”号，选中要显示的列为“月、日、凭证号、摘要、借金额和贷金额”。
4、单击“下一步”按钮，完成要显示的列的选择，进入“查询向导-筛选数据”对话框，直接单击“下一步”按钮，跳过筛选的过程，打开“查询向导-排序顺序”对话框，“主要关键字”设置为“凭证号”，完成排序的操作。
5、单击“下一步”按钮，打开“查询向导-完成”对话框，在“请确定下一步的动作”中，选中“Microsoft Query 中查看数据或编辑查询”，点击“完成”按钮，完成查询向导的操作，进入Microsoft Query界面。
6、在Microsoft Query窗口中，执行“条件”菜单下的“添加条件”命令，打开“添加条件”对话框，选择“字段”为“月”，“指定值”为“[yue]”，完成第一个参与条件的设置。
7、点击“添加”按钮，打开“输入参数值”对话框，此时不需要设置任何参数，单击“确定”按钮，完成第一个参数的设置。
8、在“添加条件”对话框中，选择“字段”为“科目代码”，“指定值”为“[kmdm]”,完成第二个参数条件的设置，单击“确定”按钮，打开“输入参数值”对话框，此时不需要设置任何参数，单击“确定”按钮，完成第二个参数的设置。再次单击“”添加条件对话框中的“关闭”按钮，完成所有参数条件的设置。
9、执行Microsoft Query窗口中文件菜单下的“将数据返回Microsoft Excel”，打开“导入数据”对话框，将数据放置的位置设置为“明细分类账”的A7单元格，完成数据导入的操作。
10、单击“导入数据”对话框中的“确定”后，打开“输入参数值”对话框，在“yue”下的文本框中，选择工作表中的F2单元格，并且勾选“在以后的刷新中使用该值或该引用”和“当单元格值更改时自动刷新”复选框。同样，在打开的指定参数kmdm中指定科目代码链接的单元格为C3单元格，完成参数的指定操作。
11、选中F7贷金额字段所在的单元格（也就是表对象的“贷金额”），右击，执行“插入”下的“在右侧插入表列”命令，插入一个新列，列名称自动设置“列1”.选中“列1”所在的单元格，右击，再次执行“插入”下的“在右侧插入列表”命令，插入一个新列，列名称为“列2”，完成列的插入操作。
12、选中A7单元格，选中“设计-表样式”中的“表样式浅色8”，完成表样式的选择。
13、选择“设计”选项卡，在“属性”组内，将表名称更改为“flz”。
14、选中第7行，右击，执行隐藏命令，完成表头行的隐藏操作。
**C、添加汇总行**
1、选中A8单元格，选中“设计”选项卡，执行“表样式选项-汇总行”命令，完成添加“汇总行”的命令。
2、删除“月”字段所在列中的“汇总”字样，在汇总行中，在“摘要”字段所在的单元格内输入文字“本月合计”。
3、在“汇总”行中，选中“借金额”所在的列，在下拉列表中选择“汇总方式”为“求和”，完成本月借方发生额的计算。
4、在“汇总”行中，选中“贷金额”所在的列，在下拉列表中选择汇总方式为“求和”，完成本月贷方发生额的计算。
5、在“汇总”行中，选中“列1”所在的列，在编辑栏中输入公式“=G6”，完成借贷方向的设置。
6、在“汇总”行中，选中“列2”所在的列，在编辑栏内输入公式“=IF($G$6="借",$H$6+flz[[#汇总],[借金额]]-flz[[#汇总],[贷金额]],H6+flz[[#汇总],[贷金额]]-flz[[#汇总],[借金额]])”，完成期末余额的计算。
7、调整各列的宽度到合适的位置，选中A7单元格，选中“设计”选项卡，单击“外部表数据”组内的属性按钮，打开“外部数据属性”对话框，保持不勾选“调整列宽”复选框，单击“确定”按钮，完成列宽格式的设置。
**D、获取最新数据**
1、选中“明细分类账”工作表，选中“开发工具-控件-插入-表单控件-按钮控件”，在H2:H3单元格内拖曳出一个命令按钮，将按钮名称更改为“数据刷新”，完成命令按钮的放置。
2、选择“开发工具-代码-录制宏”命令，打开“录制宏”对话框，将宏名称更改为“刷新明细分类账”，单击“确定”按钮，开始录制宏。
3、选中A8单元格，单击“数据”选项卡，执行“连接”组内的“全部刷新”命令，完成数据刷新操作。
4、单击“开发工具-代码组-停止录制”命令，完成宏的录制。
5、在“刷新数据”按钮上右击，执行“指定宏”命令，打开“指定宏”对话框，选择“刷新明细分类账”项，单击“确定”按钮，完成为按钮指定宏的操作。
**九、设计导航页面**
（一）实验目的
掌握利用导航页面来完成在各个页面之间的跳转。
（二）实验要求
了解链接的方法。
（三）实验内容及数据来源
导航页面要实现的功能包括：
（1）通过导航快速到达指定的页面。
（2）科目汇总表、科目余额表、总分类账和明细分类账的月份信息与导航页面的月份信息一致。
（四）实验步骤
**A、导航页面**
导航页面界面如下图所示：
1、新建一张工作表，将其命名为“首页”。
2、点击“文件”按钮，单击“Excel选项”，在对话框左侧选择“高级”，在右侧的“此工作表的显示选项”下保持“显示行和列标题”和“显示网格线”复选框不被选中，单击“确定”按钮，完成去除网格和行列标题的操作。
3、选择“插入”选项卡，执行“插图-联机图片”命令，在打开的搜索对话框中，在搜索范围内输入“计算机”，按Enter键确认后，选择一张合适的图片，单击“插入”按钮，完成图片的插入操作。
4、将图片放置在合适的位置，并调整到合适的大小，在图下输入“账务处理”
**B、设置导航功能**
1、在工作表的H8单元格内输入“凭证处理”。
2、在工作表的I8单元格内输入“凭证录入”，选中“插入”选项卡，执行“链接-超链接”命令，打开“编辑超链接”对话框，在左侧选择链接到的位置为“本文档中的位置”，右侧指定文档中选择的位置为“凭证输入”工作表，单击“确定”按钮，完成凭证输入链接的指定。
3、在工作表J8单元格输入“凭证编辑”，同样的方法指定链接到“凭证编辑”工作表，完成凭证编辑链接的指定。
4、在工作表K8单元格内输入“审核打印”，同样的方法指定链接到“凭证审核与打印”工作表，完成凭证审核与打印链接的指定。
5、在工作表G10单元格内输入“查询月份”，选中H10单元格，设置边框为双下画线，选中“数据”选项卡，执行“数据-数据验证”命令，在“数据验证”对话框的“设置”选项卡的“允许”下，选择“序列”，序列的内容手工输入“1,2,3,4,5,6,7,8,9,10,11,12”。选中“出错警告”选项卡，标题为“月份错误”，错误信息指定为“指定的月份信息错误”，单击“确定”按钮，完成月份的指定。
6、在工作表I10单元格内输入“科目余额表”，同样的方法指定链接到“科目余额表”工作表，完成科目余额表链接的指定。
7、在工作表J10单元格内输入“总分类账”，同样的方法指定链接到“总分类账”工作表，完成总分类账链接的指定。
8、在工作表K10单元格内输入“明细分类账”，同样的方法指定链接到“明细分类账”工作表，完成明细分类账链接的指定。
9、选中“科目汇总表”，选中D2单元格，在编辑栏内输入公式：
“=首页!H10”，完成月份的指定。
10、选中“科目余额表”工作表，选中G2单元格，在编辑栏内输入公式：“=首页!H10”，完成查询月份的指定。
11、选中“总分类账”工作表，选中F2单元格，在编辑栏内输入公式：“=首页!H10”，完成查询月份的指定。
12、选中“明细分类账”工作表，选中F2单元格，在编辑栏内输入公式：“=首页!H10”，完成查询月份的指定。
到此我们已经完成了会计基本核算的基本内容，结合教材大家多加练习，后续我们就不再按照我当初的设计进行教学活动。
2022年04月19日